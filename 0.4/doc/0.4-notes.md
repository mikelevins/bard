# Bard 0.4 Notes

Notes on ideas for the 0.4 version of Bard.

## Functions and dispatch

Bard functions are **polymorphic** in all their arguments: they examine all their arguments to determine the code to execute. The exact method they use to determine the code to apply depends on a dispatching rule defined separately for each function.

### Simple functions

The simplest and most efficient case is a **simple function**. A simple function performs no dispatching; it calls the same code regardless of the arguments  passed to it. Here's an example of defining a simple function:

    (define function (multiply x y)
      (* x y))

With this definition, `multiply` always executes the same code for any arguments passed to it: it applies the `*` function to the arguments.

    > (multiply 4 5)
    20

### Generic functions

A **generic function** is a function that has at least one definition that **specializes** it for at least one argument. When a function is generic, it must examine its arguments to determine which of its methods to apply. Giving a definition that specializes an argument converts an existing function to a generic function.

Under the hood, a generic function's body is a dispatching procedure that examines the arguments and chooses the appropriate code to run. The exact dispatching procedure depends on the types of definitions that have been given for the function.

Generic functions try a series of increasingly-general matching rules: first **singleton** matching, then **schema** matching, then **class** matching, then **criterion** matching, and finally **predicate dispatch**. Bard can tell statically at definition time whether a function needs to check each of these rules, based on the definitions that have been given for the function, so at runtime your code pays the cost for each type of dispatching only if you choose to enable it.

### Singleton-matching 

The most specific kind of specialization is a **singleton** specialization:

    (define function (multiply x y)
      with: ((x (exactly 0)0)))
      nothing)

The `exactly` constraint says that when `x` is equal to the value `0`, the `multiply` function should return `nothing`.

    > (multiply 0 5)
    nothing
    
If no singleton specializations are defined, or if the defined ones don't match, then the function falls back to the remaining matching rules. FOr example, if we call `multiply` with any other value as the first parameter then it falls back to the simple definition.

    > (multiply 4 5)
    20

Because the `*` function is defined only on numbers, if we try to use `multiply` on some other type of value, Bard signals an error:

    > (multiply 4 "Banana")
    ERROR: no applicable method on multiply for types (<fixnum> <string>)

### Schema matching 

A more general specialization than singleton matching is **schema** matching. If we specialize the function's arguments on schemas then it collects the schemas of its arguments and searches for definitions given for those schemas. For example, we can  define `multiply` to work on a `<fixnum>` and a `<string>`, and then the "Banana" example will work:

    (define function (multiply x y)
      with: ((x <fixnum>)(y <string>))
      (take x (repeat y)))

    > (multiply 4 "Banana")
    ("Banana" "Banana" "Banana" "Banana")


### Class matching 

More general than schema matching is **class** matching. If we give a definition that specializes on a class then the function will match its arguments against any schema that belongs to the class.

Suppose, for example, that we want to call `multiply` with a second argument that can be an instance of any member of `List`. The `<string>` schema is a member of `List`, but a definition specizlized for `<string>` only matches instances of `<string>`; it won't for example, match a list like `[1 2 3]`. TO do that, we need to instead provide a class-matching definition:

    (define function (multiply x y)
      with: ((x <fixnum>)(y List))
      (take x (repeat y)))

    > (multiply 4 [1 2 3])
    ((1 2 3) (1 2 3) (1 2 3) (1 2 3))

This definition works with any `y` that is an instance of a schema that is a member of `List`--for example, it will work for `<string>`, `<pair>`, `<bitvector>`, `<vector>`, or `<frame>`.

In Bard 0.4 the classes are arranged in a graph. For dispatching purposes, the **C3** algorithm is used to stably linearize the graph so that dispatch is always deterministic, unless you try to define a sublass/superclass relation that makes dispatch ambiguous. Bard catches such cases at definition time and prevents such definitions.

The root of the class graph, and the most inclusive of all classes, as the class Anything.

There is also a special class, called `Null`, with one member, the schema `<null>`. The schema in turn has one member, the value `nothing`. What makes `Null` special is that it's a subtype of every Bard type. `nothing` is an instance of every schema, and `<null>` is a member of every class.

### Criterion matching 

A **criterion** is a **type-defining predicate**; it's a function that defines a **computed type** by returning true or false when applied to any Bard value. If it returns true then the value is an instance of the computed type; if it returns false then the value isn't an instance of the type.

A type defined this way is called a **category**. A category can include any set of Bard values. For example, we can define the category `?Odd` like this:

    (def ?Odd (category odd-number?))
    
The function `odd?-number` is the criterion that defines the category `?Odd`. Here's a set of definitions for `odd-number?`:

    (define function (odd-number? x)
      with: ((x Anything))
      false)
      
    (define function (odd-number? x)
      with: ((x Integer))
      (odd? x))


Taken together, the two function definitions and the `def` form create a new category called `?Odd`. All odd integers are members of the new type.

We can specialize a function on a category by giving a definition that uses a `satisfies` constraint that mentions the corresponding criterion. For example:

    (define function (multiply x y)
      with: ((x (satisfies odd-number?))(y List))
      (display "That's odd!")
      (take x (repeat y)))

With this definition, applying `multiply` to an instance of `?Odd` and a `List` causes it to print a silly message before returning its result.

Bard treats criterion matches as more specific than any other kind of specializations except singleton matches.

### Predicate dispatch

The most flexible and general of all possible matching rules is **predicate dispatch**. A definition with a `when:in:` constraint converts a function to predicate dispatch, erasing all other specializations. Because predicate dispatch is so flexible and general, and because dispatching predicates can subsume and conflict with other dispatching rules, a function that uses predicate dispatch is a distinct kind of function that can have no other specializations.

A `when:in:` constraint is a predicate (`when:`) chosen from a set of predicates (`in:`)
defined for the function that accepts a list of values and returns
true or false. When the function is applied, it in turn applies its
`when:` predicate to the argument values. If the predicate returns true
then the definition is matched and its code is called; otherwise the
match fails and Bard's dispatch procedure continues searching.

If additional predicates are found in the predicate set then they are
checked in the defined order.

If no defined predicate matches then Bard calls `no-applicable-method`.

For example, suppose we want to define a function that has three different behaviors:

1. what to do when the arguments are monotonically-increasing numbers
2. what to do when the arguments are all strings
3. what to do in any other case

We start by defining the predicates:

    (define method (any-old-values? & vals) true)
    (define method (all-strings? & vals) (every? string? vals))
    (define method (monotonically-increasing? & vals) (apply < vals))
    
Next we define a predicate set that lists them in the order we want them checked:

    (def frob-predicates (predicate-set monotonically-increasing? all-strings? any-old-values?)
    
Finally, we give definitions that use the predicates:  
  
    (define method (frob & vals)
      when: monotonically-increasing?
      in: frob-predicates
      ...)
          
    (define method (frob & vals)
      when: all-strings?
      in: frob-predicates
      ...)
          
    (define method (frob & vals)
      when: any-old-values?
      in: frob-predicates
      ...)
      
## Types

Bard 0.4 has several kinds of types. At the top level, types are specifications for related groups of values. Bard provides a spectrum of abstraction ranging from completely abstract types defined only by rules for determining dynamically whether a value is an instance of the type down to purely concrete types that specify the layout of buts and bytes.

### Schemas

A **schema** is a named specification for a concrete type. It describes a representation for a set of values, specifying how its buts and bytes are laid out.

### Classes

A class is a named collection of types. It collects related abstract and concrete types into a family.

Classes are also used in defining protocols: a protocol defines a set of function signatures; each signature identifies the types of a function's inputs and outputs. Those types are given as classes.

A class is purely abstract; there can be no direct instances of a class. Instead, all concrete values are instances of schemas, and each schema may belong to one or more classes.

You can explicitly declare that a schema or other type is a member of a class, but types can become members of classes automatically, too. When you define a protocol, you give the input and output types of its functions as classes. When you specialize one of those functions for a schema, the schema becomes a member of the class of the corresponding parameter.

In summary: a class is a named collection of types. Its membership can change over time, either explicitly through declarations or implicitly as an effect of function definitions.

### Categories

A category is a type defined by a criterion. A criterion is a predicate that returns true for values that belong to the category and false for other values. Any value may be an instance of a category; the sole discriminating factor is the criterion function.

### Built-in and user-defined types

Bard provides a useful set of built-in schemas, built-in protocols and classes that organize them into useful families, and several categories provided to make certain types of operations convenient (for example, the `?Alphanumeric` and `?Whitespace` categories, which identify certain characters).

Users have several options for creating types. You can write criterion functions to create categories. You can define protocols and classes. You can also use **type definitions** to create new types.

### Schema definitions

A **schema definition** creates a new schema that describes how Bard values can be combined into new structures to represent new concrete types. The schema-definition language is a small language loosely based on Common Lisp type descriptions and Haskell datatypes.

A `schema` form describes a schema:

    (schema <fixnum>)
    (schema (<vector> <unsigned-byte> 4))

You can define a schema by using `def` to bind the value of a `schema` form to a variable:

    (def <ostype> (schema (<vector> <unsigned-byte> 4)))
    
Alternatively, you can combine the creation of the schema and the variable, and make it more obvious what you're doing by using the `define schema` special form:

    (define schema <ostype> (<vector> <unsigned-byte> 4))

The argument to `schema` is a description of the concrete layout of the type. For example,

    (schema <fixnum>)

describes an integer value represented by an instance of `<fixnum>`.

    (schema (<vector> <unsigned-byte> 4))

describes a value that is a vector of length 4 whose elements are `<unsigned-byte>` values. In general, atomic values are given as a schema or as an expression of the form

    (or schema1 schema2 ... schemaN)
    
In the latter case, an instance of any of the arguments to `or` is a valid value for the schema.

The argument to `schema` can also be either a `record` expression or a `union` expression.